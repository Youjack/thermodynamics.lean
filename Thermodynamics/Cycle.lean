import Mathlib.Logic.Equiv.TransferInstance
import Thermodynamics.MathLemma
import Thermodynamics.ZerothLaw

/-!
# Thermodynamic Cycles and the FIRST Law

This file defines
* the notion of abstract thermodynamic `Cycle`s, which satisfy the first law
* some specific types of `Cycle`s, including
  `OneRsvCycle`, `AbsRelCycle`, `class EngineCycle`,
  `UsualEngineCycle`, `UsualPumpCycle`
* the connection (`add`), scaling (`smul`), and reversion of `Cycle`s
and proves
* some basic properties of the various types of `Cycle`s
-/

noncomputable section

namespace Thermodynamics

/-!
## General abstract `Cycle`

According to the first law, a thermodynamic `Cycle` can be regarded as a heat-work conversion.
* Abstract `Cycle`s are defined to be the equivalence class of concrete cycles
  that have the same net heat-work conversion.
* `Cycle`s need not to satisfy the second law.
---------------------------------------------------------------------------------------------------/

/-- Abstrct `Cycle` (denoted by `H`) :
    finitely many `ğ“£`s PROVIDE heat `ğ“  ğ“£`, and `H` DOES work `ğ“¦`, satisfying the first law -/
structure Cycle where
  ğ“  : Reservoir â†’â‚€ â„
  ğ“¦ : â„
  energy_conserv : ğ“ .sum_image - ğ“¦ = 0

namespace Cycle

variable (H : Cycle)
/-- `abs`orbed heat `Qabs = âˆ‘(+ğ“  > 0)` -/
def Qabs : â„ := H.ğ“ .support.sum (fun ğ“£ â†¦ max (H.ğ“  ğ“£) 0)
lemma Qabs_nonneg : 0 â‰¤ H.Qabs := by simp [Qabs, Finset.sum_nonneg]
/-- `rel`eased heat `Qrel = âˆ‘(-ğ“  > 0)` -/
def Qrel : â„ := H.ğ“ .support.sum (fun ğ“£ â†¦ max (- H.ğ“  ğ“£) 0)
lemma Qrel_nonneg : 0 â‰¤ H.Qrel := by simp [Qrel, Finset.sum_nonneg]

variable {H}
lemma ğ“¦_from_ğ“  : H.ğ“¦ = H.ğ“ .sum_image := by
  have := H.energy_conserv; rw [sub_eq_zero] at this; rw [this]
lemma ğ“ _from_Qabs_Qrel : H.ğ“ .sum_image = H.Qabs - H.Qrel := by simp [Qabs, Qrel, â†Finset.sum_sub_distrib]
lemma ğ“¦_from_Qabs_Qrel : H.ğ“¦   = H.Qabs - H.Qrel := by rw [ğ“¦_from_ğ“ , ğ“ _from_Qabs_Qrel]
lemma Qabs_from_ğ“¦_Qrel : H.Qabs = H.ğ“¦   + H.Qrel := by simp [ğ“¦_from_Qabs_Qrel]
lemma Qrel_from_Qabs_ğ“¦ : H.Qrel = H.Qabs - H.ğ“¦   := by simp [ğ“¦_from_Qabs_Qrel]

/-- A `Cycle` is fully specified by `ğ“ `  -/
@[reducible] def equiv_ğ“  : (Reservoir â†’â‚€ â„) â‰ƒ Cycle where
  toFun := fun ğ“  â†¦ {
    ğ“  := ğ“ 
    ğ“¦ := ğ“ .sum_image
    energy_conserv := by rw [sub_self]
  }
  invFun := fun H â†¦ H.ğ“ 
  left_inv := fun H â†¦ rfl
  right_inv := fun H â†¦ by
    let âŸ¨ğ“ ,ğ“¦,energy_conservâŸ© := H; simp
    rwa [sub_eq_zero] at energy_conserv
/-- `Cycle` forms an `AddCommGroup`.
* adding two `Cycle`s means connecting them
* negating a `Cycle` means reversing it
-/
instance : AddCommGroup Cycle := Equiv.addCommGroup equiv_ğ“ .symm
/-- `Cycle` forms a real vector space.
* multiplying a `Cycle` with `c âˆˆ â„` means scaling it `c` times
-/
instance : Module â„     Cycle := Equiv.module â„    equiv_ğ“ .symm

variable {Hâ‚ Hâ‚‚ : Cycle} {c : â„}
@[simp] lemma ğ“¦_add : (Hâ‚ + Hâ‚‚).ğ“¦ = Hâ‚.ğ“¦ + Hâ‚‚.ğ“¦ := calc
  _ = Hâ‚.ğ“ .sum_image + Hâ‚‚.ğ“ .sum_image := Finsupp.sum_image_add
  _ = Hâ‚.ğ“¦ + Hâ‚‚.ğ“¦ := by simp [ğ“¦_from_ğ“ ]
@[simp] lemma ğ“¦_smul : (c â€¢ H).ğ“¦ = c * H.ğ“¦ := calc
  _ = c * H.ğ“ .sum_image := Finsupp.sum_image_smul
  _ = c * H.ğ“¦ := by rw [ğ“¦_from_ğ“ ]
@[simp] lemma rev_ğ“¦ : (-H).ğ“¦ = -H.ğ“¦ := calc
  _ = ((-1:â„) â€¢ H).ğ“¦ := by simp
  _ = (-1:â„) * H.ğ“¦ := by rw [ğ“¦_smul]
  _ = -H.ğ“¦ := by simp
@[simp] lemma Qabs_smul_pos (hc : 0 < c) : (c â€¢ H).Qabs = c * H.Qabs := calc
  _ = (c â€¢ H.ğ“ ).support.sum (fun ğ“£ â†¦ max ((c â€¢ H).ğ“  ğ“£) 0) := rfl
  _ =       H.ğ“ .support.sum (fun ğ“£ â†¦ max ((c â€¢ H).ğ“  ğ“£) 0) := by simp [ne_of_gt hc]
  _ =       H.ğ“ .support.sum (fun ğ“£ â†¦ max  (c *  H.ğ“  ğ“£) 0) := rfl
  _ =       H.ğ“ .support.sum (fun ğ“£ â†¦ c * max   (H.ğ“  ğ“£) 0) := by simp [le_of_lt hc]
  _ =  c *  H.ğ“ .support.sum (fun ğ“£ â†¦     max   (H.ğ“  ğ“£) 0) := by rw [Finset.mul_sum]
  _ =  c *  H.Qabs := rfl
@[simp] lemma Qrel_smul_pos (hc : 0 < c) : (c â€¢ H).Qrel = c * H.Qrel := calc
  _ = (c â€¢ H.ğ“ ).support.sum (fun ğ“£ â†¦ max (- (c â€¢ H).ğ“  ğ“£) 0) := rfl
  _ =       H.ğ“ .support.sum (fun ğ“£ â†¦ max (- (c â€¢ H).ğ“  ğ“£) 0) := by simp [ne_of_gt hc]
  _ =       H.ğ“ .support.sum (fun ğ“£ â†¦ max (- (c * H.ğ“  ğ“£)) 0) := rfl
  _ =       H.ğ“ .support.sum (fun ğ“£ â†¦ max (c * (- H.ğ“  ğ“£)) 0) := by simp
  _ =       H.ğ“ .support.sum (fun ğ“£ â†¦ c * max ((- H.ğ“  ğ“£)) 0) := by simp only [le_of_lt hc, Real.max_mul_nonneg_zero]
  _ =  c *  H.ğ“ .support.sum (fun ğ“£ â†¦     max ((- H.ğ“  ğ“£)) 0) := by rw [Finset.mul_sum]
  _ =  c *  H.Qrel := rfl
lemma rev_Qabs : (-H).Qabs = H.Qrel := calc
  _ = (-H.ğ“ ).support.sum (fun ğ“£ â†¦ max ((-H).ğ“  ğ“£) 0) := rfl
  _ =    H.ğ“ .support.sum (fun ğ“£ â†¦ max ((-H).ğ“  ğ“£) 0) := by simp
  _ = H.Qrel := rfl
lemma rev_Qrel : (-H).Qrel = H.Qabs := calc
  _ = (-H.ğ“ ).support.sum (fun ğ“£ â†¦ max (-(-H).ğ“  ğ“£) 0) := rfl
  _ =    H.ğ“ .support.sum (fun ğ“£ â†¦ max (-(-H).ğ“  ğ“£) 0) := by simp
  _ =    H.ğ“ .support.sum (fun ğ“£ â†¦ max (- - H.ğ“  ğ“£) 0) := rfl
  _ =    H.ğ“ .support.sum (fun ğ“£ â†¦ max (    H.ğ“  ğ“£) 0) := by simp
  _ = H.Qabs := rfl

end Cycle

/-!
## Some types of abstract `Cycle`s
---------------------------------------------------------------------------------------------------/

/-!
### `OneRsvCycle`
-/

/-- `Cycle`s that exchange heat with only one `Reservoir` -/
structure OneRsvCycle (ğ“£ : Reservoir) extends Cycle where
  one_rsv : ğ“ .support = {ğ“£}

namespace OneRsvCycle
variable {ğ“£ : Reservoir} {H : OneRsvCycle ğ“£}

lemma Qabs_one_rsv : H.Qabs = max (  H.ğ“  ğ“£) 0 := by simp [Cycle.Qabs, one_rsv]
lemma Qrel_one_rsv : H.Qrel = max (- H.ğ“  ğ“£) 0 := by simp [Cycle.Qrel, one_rsv]
/-- `ğ“¦` is `conv`erted from/to only one rsv. -/
lemma ğ“¦_conv_one_rsv : H.ğ“¦ = H.ğ“  ğ“£ := by simp [Cycle.ğ“¦_from_ğ“ , Finsupp.sum_image, one_rsv]

lemma toCycle_inj : Function.Injective (fun H : OneRsvCycle ğ“£ â†¦ H.toCycle) :=
  Î» âŸ¨_,_âŸ© âŸ¨_,_âŸ© â†¦ by simp
instance : SMul â„>0 (OneRsvCycle ğ“£) where
  smul := fun c H â†¦ {
    (c:â„) â€¢ H.toCycle with
    one_rsv := calc
      _ = ((c:â„) â€¢ H.toCycle.ğ“ ).support := rfl
      _ =           H.toCycle.ğ“ .support := by simp [ne_of_gt c.property]
      _ = {ğ“£} := H.one_rsv
  }

end OneRsvCycle

/-!
### `AbsRelCycle`
-/

/-- `Cycle`s that absorb heat `Qabs > 0` from rsv `ğ“` and release heat `Qrel > 0` to rsv `ğ“¡` -/
structure AbsRelCycle (ğ“ ğ“¡ : Reservoir) extends Cycle where
  two_rsv : ğ“ .support = {ğ“, ğ“¡} âˆ§ ğ“ â‰  ğ“¡
  do_abs_rsv : 0 < ğ“  ğ“ -- does absorb  heat from `ğ“`
  do_rel_rsv : ğ“  ğ“¡ < 0 -- does release heat to   `ğ“¡`

namespace AbsRelCycle

variable {ğ“ ğ“¡ : Reservoir} {H : AbsRelCycle ğ“ ğ“¡}
lemma Qabs_one_rsv : H.Qabs = H.ğ“  ğ“ := calc
  _ = ({ğ“, ğ“¡}:Finset _).sum (fun ğ“£ â†¦ max (H.ğ“  ğ“£) 0) := by simp [Cycle.Qabs, two_rsv]
  _ = H.ğ“  ğ“ := by
    rw [Finset.sum_pair]
    . have hğ“ : max (H.ğ“  ğ“) 0 = H.ğ“  ğ“ := by simp [le_of_lt H.do_abs_rsv]
      have hğ“¡ : max (H.ğ“  ğ“¡) 0 =      0 := by simp [le_of_lt H.do_rel_rsv]
      simp [hğ“, hğ“¡]
    . exact H.two_rsv.2
lemma Qrel_one_rsv : H.Qrel = - H.ğ“  ğ“¡ := calc
  _ = ({ğ“, ğ“¡}:Finset _).sum (fun ğ“£ â†¦ max (- H.ğ“  ğ“£) 0) := by simp [Cycle.Qrel, two_rsv]
  _ = - H.ğ“  ğ“¡ := by
    rw [Finset.sum_pair]
    . have hğ“ : max (- H.ğ“  ğ“) 0 =       0 := by simp [le_of_lt H.do_abs_rsv]
      have hğ“¡ : max (- H.ğ“  ğ“¡) 0 = - H.ğ“  ğ“¡ := by simp [le_of_lt H.do_rel_rsv]
      simp [hğ“, hğ“¡]
    . exact H.two_rsv.2
lemma do_abs : 0 < H.Qabs := calc
  _ = H.ğ“  ğ“ := Qabs_one_rsv
  _ > 0 := H.do_abs_rsv
lemma do_rel : 0 < H.Qrel := calc
  _ = - H.ğ“  ğ“¡ := Qrel_one_rsv
  _ > 0 := by simp [do_rel_rsv]
lemma ğ“¦_from_two_rsv : H.ğ“¦ = (H.ğ“  ğ“) + (H.ğ“  ğ“¡) := calc
  _ =  H.Qabs  -  H.Qrel := Cycle.ğ“¦_from_Qabs_Qrel
  _ = (H.ğ“  ğ“) + (H.ğ“  ğ“¡) := by simp [Qabs_one_rsv, Qrel_one_rsv]
lemma Qabs_lt_Qrel_iff_consume_work : (H.Qabs < H.Qrel) â†” (H.ğ“¦ < 0) := by
  constructor
  . intro do_pump
    calc
      _ = H.Qabs - H.Qrel := Cycle.ğ“¦_from_Qabs_Qrel
      _ < 0 := by simp [do_pump]
  . intro consume_work
    rw [Cycle.ğ“¦_from_Qabs_Qrel] at consume_work
    simpa using consume_work

lemma toCycle_inj : Function.Injective (fun H : AbsRelCycle ğ“ ğ“¡ â†¦ H.toCycle) :=
  Î» âŸ¨_,_,_,_âŸ© âŸ¨_,_,_,_âŸ© â†¦ by simp

variable {Hâ‚ Hâ‚‚ : AbsRelCycle ğ“ ğ“¡}
lemma eq_of_Q_eq (habs : Hâ‚.Qabs = Hâ‚‚.Qabs) (hrel : Hâ‚.Qrel = Hâ‚‚.Qrel) : Hâ‚ = Hâ‚‚ := by
  suffices Hâ‚.ğ“  = Hâ‚‚.ğ“  from by
    apply toCycle_inj; apply Cycle.equiv_ğ“ .symm.injective
    exact this
  apply Finsupp.ext_iff'.mpr; constructor
  . simp [two_rsv]
  . simp [two_rsv]; constructor
    . rw [â†Hâ‚.Qabs_one_rsv, â†Hâ‚‚.Qabs_one_rsv]; exact habs
    . rw [Hâ‚.Qrel_one_rsv, Hâ‚‚.Qrel_one_rsv] at hrel; simpa using hrel
lemma eq_of_Qabs_ğ“¦_eq (hQabs : Hâ‚.Qabs = Hâ‚‚.Qabs) (hğ“¦ : Hâ‚.ğ“¦ = Hâ‚‚.ğ“¦) : Hâ‚ = Hâ‚‚ := by
  refine eq_of_Q_eq hQabs ?_
  rw [Hâ‚.ğ“¦_from_Qabs_Qrel, Hâ‚‚.ğ“¦_from_Qabs_Qrel] at hğ“¦
  linarith

instance : Add (AbsRelCycle ğ“ ğ“¡) where
  add := fun Hâ‚ Hâ‚‚ â†¦
    have := Hâ‚.do_abs_rsv; have := Hâ‚.do_rel_rsv
    have := Hâ‚‚.do_abs_rsv; have := Hâ‚‚.do_rel_rsv
    { Hâ‚.toCycle + Hâ‚‚.toCycle with
      two_rsv := by
        constructor
        . apply Finsupp.support_add_exact
          . simp; constructor
            . linarith
            . linarith
          . have := calc (Hâ‚.ğ“ .support âˆª Hâ‚‚.ğ“ .support) \ {ğ“, ğ“¡}
              _ = ({ğ“, ğ“¡} âˆª {ğ“, ğ“¡}) \ {ğ“, ğ“¡} := by rw [Hâ‚.two_rsv.1, Hâ‚‚.two_rsv.1]
              _ = âˆ… := by simp
            simp [this]
        . exact Hâ‚.two_rsv.2
      do_abs_rsv := calc (Hâ‚.toCycle + Hâ‚‚.toCycle).ğ“  ğ“
        _ = Hâ‚.ğ“  ğ“ + Hâ‚‚.ğ“  ğ“ := rfl
        _ > 0 := by linarith
      do_rel_rsv := calc (Hâ‚.toCycle + Hâ‚‚.toCycle).ğ“  ğ“¡
        _ = Hâ‚.ğ“  ğ“¡ + Hâ‚‚.ğ“  ğ“¡ := rfl
        _ < 0 := by linarith
    }
instance : AddCommSemigroup (AbsRelCycle ğ“ ğ“¡) :=
  Function.Injective.addCommSemigroup _ toCycle_inj (Î» _ _ â†¦ rfl)
instance : SMul â„>0 (AbsRelCycle ğ“ ğ“¡) where
  smul := fun c H â†¦ {
    c.val â€¢ H.toCycle with
    two_rsv := by
      constructor
      . simp
        calc (c.val â€¢ H.toCycle).ğ“ .support
          _ = (c.val â€¢ H.ğ“ ).support := rfl
          _ =          H.ğ“ .support := by simp [Finsupp.support_smul_eq, ne_of_gt c.property]
          _ = {ğ“, ğ“¡} := H.two_rsv.1
      . exact H.two_rsv.2
    do_abs_rsv := calc (c.val â€¢ H.toCycle).ğ“  ğ“
      _ = c * H.ğ“  ğ“ := rfl
      _ > 0 := by simp [c.property, do_abs_rsv]
    do_rel_rsv := calc (c.val â€¢ H.toCycle).ğ“  ğ“¡
      _ = c * H.ğ“  ğ“¡ := rfl
      _ < 0 := mul_neg_of_pos_of_neg c.property H.do_rel_rsv
  }

/-- The reverse of an `AbsRelCycle ğ“ ğ“¡` is an `AbsRelCycle ğ“¡ ğ“`. -/
@[reducible] def rev (H : AbsRelCycle ğ“ ğ“¡) : AbsRelCycle ğ“¡ ğ“ := {
  -H.toCycle with
  two_rsv := by
    constructor
    . calc (-H.toCycle).ğ“ .support
        _ = (-H.ğ“ ).support := rfl
        _ = (H.ğ“ ).support := by rw [Finsupp.support_neg]
        _ = {ğ“¡, ğ“} := by
          rw [H.two_rsv.1]
          ext; simp; tauto
    . exact H.two_rsv.2.symm
  do_abs_rsv := calc (-H.toCycle).ğ“  ğ“¡
    _ = - H.ğ“  ğ“¡ := rfl
    _ = H.Qrel := AbsRelCycle.Qrel_one_rsv.symm
    _ > 0 := H.do_rel
  do_rel_rsv := calc (-H.toCycle).ğ“  ğ“
    _ = - H.ğ“  ğ“ := rfl
    _ = - H.Qabs := by rw [AbsRelCycle.Qabs_one_rsv]
    _ < 0 := by simpa using AbsRelCycle.do_abs
}

end AbsRelCycle

/-!
### `EngineCycle` class
-/

/-- `Cycle`s that do positive work -/
class EngineCycle (H : Cycle) where
  do_work : 0 < H.ğ“¦

namespace Cycle
variable {H : Cycle} [EngineCycle H]

lemma engine_do_work : 0 < H.ğ“¦ := EngineCycle.do_work
lemma engine_do_abs : 0 < H.Qabs := calc
  _ = H.ğ“¦ + H.Qrel := H.Qabs_from_ğ“¦_Qrel
  _ > 0 := by
    have := H.engine_do_work; have := H.Qrel_nonneg
    positivity

/-- `eff`iciency of `engine_cycle` -/
def eff (H : Cycle) [EngineCycle H] : â„ := H.ğ“¦ / H.Qabs
lemma eff_from_Qabs_Qrel : H.eff = 1 - H.Qrel / H.Qabs := calc
  _ = H.ğ“¦ / H.Qabs := rfl
  _ = ( H.Qabs - H.Qrel ) / H.Qabs := by rw [Cycle.ğ“¦_from_Qabs_Qrel]
  _ = 1 - H.Qrel / H.Qabs := by field_simp [ne_of_gt H.engine_do_abs]
lemma ğ“¦_from_eff_Qabs : H.ğ“¦ = H.eff * H.Qabs := symm <| calc
  _ = H.ğ“¦ / H.Qabs * H.Qabs := rfl
  _ = H.ğ“¦ := by field_simp [ne_of_gt H.engine_do_abs]
lemma Qabs_from_ğ“¦_eff : H.Qabs = H.ğ“¦ / H.eff := symm <| calc
  _ = H.ğ“¦ / ( H.ğ“¦ / H.Qabs ) := rfl
  _ = H.Qabs := by field_simp [ne_of_gt H.engine_do_work]; ring
lemma eff_pos    : 0 < H.eff := by
  have := H.engine_do_work; have := H.engine_do_abs
  unfold eff; positivity
lemma eff_le_one : H.eff â‰¤ 1 := calc H.eff
  _ = 1 - H.Qrel / H.Qabs := H.eff_from_Qabs_Qrel
  _ â‰¤ 1 := by
    simp
    have := H.Qrel_nonneg; have := H.engine_do_abs
    positivity

variable {Hâ‚ Hâ‚‚ : Cycle} [EngineCycle Hâ‚] [EngineCycle Hâ‚‚]
instance engine_add : EngineCycle (Hâ‚ + Hâ‚‚) where
  do_work := calc (Hâ‚ + Hâ‚‚).ğ“¦
    _ = Hâ‚.ğ“¦ + Hâ‚‚.ğ“¦ := by simp
    _ > 0 := by
      have := Hâ‚.engine_do_work; have := Hâ‚‚.engine_do_work
      positivity
variable {c : â„} [hc : Fact (0 < c)]
instance engine_smul_pos : EngineCycle (c â€¢ H) where
  do_work := calc (c â€¢ H).ğ“¦
    _ = c * H.ğ“¦ := by simp
    _ > 0 := by
      have := hc.out; have := H.engine_do_work
      positivity
lemma eff_smul_pos : (c â€¢ H).eff = H.eff := calc
  _ = (c â€¢ H).ğ“¦ / (c â€¢ H).Qabs := rfl
  _ = (c * H.ğ“¦) / (c * H.Qabs) := by simp [hc.out]
  _ =      H.ğ“¦  /      H.Qabs  := by
    field_simp [ne_of_gt hc.out, ne_of_gt H.engine_do_abs]; ring
  _ = H.eff := rfl

end Cycle

/-!
### `UsualEngineCycle`
-/

/-- `AbsRelCycle`s that abs from hotter `ğ“—`, rel to colder `ğ“’`, and do positive work -/
structure UsualEngineCycle {ğ“— ğ“’ : Reservoir} (_: ğ“’ < ğ“—) extends (AbsRelCycle ğ“— ğ“’) where
  do_work : 0 < ğ“¦

namespace UsualEngineCycle
variable {ğ“— ğ“’} {ğ“’_lt_ğ“— : ğ“’ < ğ“—} {H : UsualEngineCycle ğ“’_lt_ğ“—}

instance : EngineCycle H.toCycle where do_work := H.do_work
lemma eff_lt_one : H.eff < 1 := calc H.eff
  _ = 1 - H.Qrel / H.Qabs := Cycle.eff_from_Qabs_Qrel
  _ < 1 := by
    simp
    have := H.do_rel; have := H.engine_do_abs
    positivity
lemma eff_from_ğ“¦_ğ“  : H.eff = H.ğ“¦ / (H.ğ“  ğ“—) := calc
  _ = H.ğ“¦ / H.Qabs := rfl
  _ = H.ğ“¦ / (H.ğ“  ğ“—) := by rw [AbsRelCycle.Qabs_one_rsv]
lemma eff_from_ğ“  : H.eff = 1 - (- H.ğ“  ğ“’)/(H.ğ“  ğ“—) := calc
  _ = 1 - H.Qrel / H.Qabs := Cycle.eff_from_Qabs_Qrel
  _ = 1 - (- H.ğ“  ğ“’)/(H.ğ“  ğ“—) := by rw [AbsRelCycle.Qabs_one_rsv, AbsRelCycle.Qrel_one_rsv]

lemma toAbsRelCycle_inj :
  Function.Injective (fun H : UsualEngineCycle ğ“’_lt_ğ“— â†¦ H.toAbsRelCycle) :=
  Î» âŸ¨_,_âŸ© âŸ¨_,_âŸ© â†¦ by simp
instance : Add (UsualEngineCycle ğ“’_lt_ğ“—) where
  add := fun Hâ‚ Hâ‚‚ â†¦ {
    Hâ‚.toAbsRelCycle + Hâ‚‚.toAbsRelCycle,
    @Cycle.engine_add Hâ‚.toCycle Hâ‚‚.toCycle _ _ with
  }
instance : AddCommSemigroup (UsualEngineCycle ğ“’_lt_ğ“—) :=
  Function.Injective.addCommSemigroup _ toAbsRelCycle_inj (Î» _ _ â†¦ rfl)
instance : SMul â„>0 (UsualEngineCycle ğ“’_lt_ğ“—) where
  smul := fun c H â†¦ {
    c â€¢ H.toAbsRelCycle,
    @Cycle.engine_smul_pos H.toCycle _ _ âŸ¨c.propertyâŸ© with
  }

-- TODO : equivalence classes of `UsualEngineCycle`
variable {Hâ‚ Hâ‚‚ : UsualEngineCycle ğ“’_lt_ğ“—}
lemma eq_smul_pos_of_eff_eq (heff : Hâ‚.eff = Hâ‚‚.eff) : âˆƒ c : â„>0, Hâ‚ = c â€¢ Hâ‚‚ := by
  let c : â„>0 := {
    val := Hâ‚.ğ“¦ / Hâ‚‚.ğ“¦
    property := by have := Hâ‚.do_work; have := Hâ‚‚.do_work; positivity
  }
  refine âŸ¨c, symm <| ?_âŸ©
  apply toAbsRelCycle_inj; apply AbsRelCycle.eq_of_Qabs_ğ“¦_eq
  Â· calc (c â€¢ Hâ‚‚).Qabs
      _ = c.val * Hâ‚‚.toCycle.Qabs := Cycle.Qabs_smul_pos c.property
      _ = Hâ‚.ğ“¦ / Hâ‚‚.ğ“¦ * Hâ‚‚.Qabs  := rfl
      _ = Hâ‚.ğ“¦ /(Hâ‚‚.ğ“¦ / Hâ‚‚.Qabs) := by field_simp
      _ = Hâ‚.ğ“¦ / Hâ‚‚.eff           := rfl
      _ = Hâ‚.ğ“¦ / Hâ‚.eff           := by rw [heff]
      _ = Hâ‚.ğ“¦ /(Hâ‚.ğ“¦ / Hâ‚.Qabs) := rfl
      _ = Hâ‚.Qabs := by
        field_simp [ne_of_gt Cycle.engine_do_work, Cycle.engine_do_abs]; ring
  Â· calc (c â€¢ Hâ‚‚).ğ“¦
      _ = (c.val â€¢ Hâ‚‚.toCycle).ğ“¦ := rfl
      _ = c.val * Hâ‚‚.ğ“¦ := by simp
      _ = Hâ‚.ğ“¦ / Hâ‚‚.ğ“¦ * Hâ‚‚.ğ“¦ := rfl
      _ = Hâ‚.ğ“¦ := by field_simp [ne_of_gt Cycle.engine_do_work]

end UsualEngineCycle

/-!
### `UsualPumpCycle`
-/

/-- `AbsRelCycle`s that pump heat from colder `ğ“’` to hotter `ğ“—` -/
structure UsualPumpCycle {ğ“’ ğ“— : Reservoir} (_: ğ“’ < ğ“—) extends (AbsRelCycle ğ“’ ğ“—) where
  do_pump : toCycle.Qabs < toCycle.Qrel

namespace UsualPumpCycle
variable {ğ“’ ğ“—} {ğ“’_lt_ğ“— : ğ“’ < ğ“—} {H : UsualPumpCycle ğ“’_lt_ğ“—}

lemma consume_work : H.ğ“¦ < 0 := AbsRelCycle.Qabs_lt_Qrel_iff_consume_work.mp H.do_pump

lemma toAbsRelCycle_inj :
  Function.Injective (fun H : UsualPumpCycle ğ“’_lt_ğ“— â†¦ H.toAbsRelCycle) :=
  Î» âŸ¨_,_âŸ© âŸ¨_,_âŸ© â†¦ by simp
instance : Add (UsualPumpCycle ğ“’_lt_ğ“—) where
  add := fun Hâ‚ Hâ‚‚ â†¦ {
    Hâ‚.toAbsRelCycle + Hâ‚‚.toAbsRelCycle with
    do_pump := by
      apply AbsRelCycle.Qabs_lt_Qrel_iff_consume_work.mpr
      calc (Hâ‚.toAbsRelCycle + Hâ‚‚.toAbsRelCycle).ğ“¦
        _ = Hâ‚.ğ“¦ + Hâ‚‚.ğ“¦ := Cycle.ğ“¦_add
        _ < 0 := by
          have := Hâ‚.consume_work; have := Hâ‚‚.consume_work
          linarith
  }
instance : AddCommSemigroup (UsualPumpCycle ğ“’_lt_ğ“—) :=
  Function.Injective.addCommSemigroup _ toAbsRelCycle_inj (Î» _ _ â†¦ rfl)
instance : SMul â„>0 (UsualPumpCycle ğ“’_lt_ğ“—) where
  smul := fun c H â†¦ {
    c â€¢ H.toAbsRelCycle with
    do_pump := by
      apply AbsRelCycle.Qabs_lt_Qrel_iff_consume_work.mpr
      calc (c â€¢ H.toAbsRelCycle).ğ“¦
        _ = c * H.ğ“¦ := Cycle.ğ“¦_smul
        _ < 0 := mul_neg_of_pos_of_neg c.property H.consume_work
  }

end UsualPumpCycle

/-!
### Reverse of `UsualEngineCycle` and `UsualPumpCycle`
-/

section RevUsualEnginePumpCycle
variable {ğ“’ ğ“— : Reservoir} {ğ“’_lt_ğ“— : ğ“’ < ğ“—}

/-- The reverse of an `UsualEngineCycle` is an `UsualPumpCycle`. -/
@[reducible] def UsualEngineCycle.rev (H : UsualEngineCycle ğ“’_lt_ğ“—) : UsualPumpCycle ğ“’_lt_ğ“— := {
  H.toAbsRelCycle.rev with
  do_pump := by
    apply AbsRelCycle.Qabs_lt_Qrel_iff_consume_work.mpr
    calc H.toAbsRelCycle.rev.ğ“¦
      _ = (-H.toCycle).ğ“¦ := rfl
      _ = -H.ğ“¦ := by simp
      _ < 0 := by simp [Cycle.engine_do_work]
}
/-- The reverse of an `UsualPumpCycle` is an `UsualEngineCycle`. -/
@[reducible] def UsualPumpCycle.rev (H : UsualPumpCycle ğ“’_lt_ğ“—) : UsualEngineCycle ğ“’_lt_ğ“— := {
  H.toAbsRelCycle.rev with
  do_work := calc H.toAbsRelCycle.rev.ğ“¦
    _ = (-H.toCycle).ğ“¦ := rfl
    _ = -H.ğ“¦ := by simp
    _ > 0 := by simp [UsualPumpCycle.consume_work]
}

end RevUsualEnginePumpCycle

end Thermodynamics
